buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url uri('../plugin/build/repo')
        }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.0'
        if (file(PREBUILT_PLUGIN_DIR).exists()) {
            classpath "ru.mail:android-kali:$thePluginVersion"
        }
    }
}

apply plugin: 'com.android.application'

repositories {
    jcenter()
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    compile 'com.android.support:appcompat-v7:25.0.1'
    testCompile 'junit:junit:4.12'
}

android {
    compileSdkVersion 23
    buildToolsVersion "25.0.0"

    defaultConfig {
        applicationId "com.example.kali"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

if (file(PREBUILT_PLUGIN_DIR).exists()) {

    apply plugin: 'ru.mail.android-kali'

    kali {
        replaceCalls {
            ignoreClasses = [
                    'com.example.kali.FakeLock',
                    'com.example.kali.ActivityLogger'
            ]
            replacements = [
                    'android.os.PowerManager$WakeLock.release()V': 'com.example.kali.FakeLock.verboseRelease',
            ]
            replacementsRegex = [
                    'android.os.PowerManager\\$WakeLock acquire \\(.+\\)V': 'com.example.kali.FakeLock.verboseAcquire',
                    '.*Activity setContentView \\(I\\)V'                  : 'com.example.kali.ActivityLogger.setContentView(Landroid/app/Activity;I)V'
            ]
        }
        inlineSyntheticFieldAccessors true
    }

} else {
    logger.warn '\n*** WARNING ***\n' +
            '  Prebuilt plugin was not found\n' +
            '  To apply the transformations you need to run:\n' +
            '    ./gradlew :plugin:uploadArchives\n' +
            '  And then rebuild the app project:\n' +
            '    ./gradlew :app:clean :app:assembleDebug\n'
}
